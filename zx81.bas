1 REM RANDOMIZATION ROUTINE
2 PRINT "PRESS ENTER:"
3 INPUT A$
4 RAND 0
10 REM VARIABLES
20 LET Z=0
30 LET Y=1
40 LET A=Z
50 LET T=Z
60 LET V=13
70 LET C=44
80 LET D=Z
90 LET E=Z
100 LET R=Z
110 LET X=2
120 LET I=Z
130 LET J=Z
140 DIM A(45)
150 LET G=100
160 LET H=4
170 LET K=512
180 LET L=32
190 LET U=16
200 LET N=255
210 LET O=99
220 LET F=65535
230 LET Q=6
240 LET S=3841
250 LET B=4096
260 LET M=39
270 LET P=Y
280 LET W=Z
290 GOTO 2520
315 CLS
320 PRINT "WELCOME TO THE BATTLE SYSTEM TEST."
330 PRINT
340 PRINT
350 PRINT "TWO SOLDIERS APPROACH."
360 PRINT
361 PRINT "PRESS A KEY TO CONTINUE."
366 SLOW
367 LET A$=INKEY$
368 IF A$="" THEN GOTO 870
369 FAST
370 REM MAIN LOOP
380 IF A((V*A)+Y)=Z THEN GOTO 420
381 PRINT "PRESS A KEY TO CONTINUE."
382 SLOW
383 LET A$=INKEY$
384 IF A$="" THEN GOTO 383
385 FAST
386 CLS
390 IF A=Z THEN GOTO 780
400 LET T=Z
410 GOTO 1190
420 REM ITERATE ATTACKER
430 IF A(Y)=Z THEN GOTO 2410
440 LET A=A+Y
450 IF A<>Z THEN LET T=Z
460 IF A((V*A)+Y)=Z THEN LET A=A+Y
470 IF A>X THEN LET A=Z
480 LET D=Z
490 IF A((V*Y)+Y)=Z THEN LET D=D+Y
500 IF A((V*X)+Y)=Z THEN LET D=D+Y
510 IF D=X THEN GOTO 530
520 GOTO 370
530 REM NEW GAME
540 PRINT "YOU ARE VICTORIOUS."
550 PRINT
560 LET W=W+Y
570 LET A=Z
580 LET T=Z
590 IF W>=10 THEN GOSUB 690
600 FOR I=Y TO X
610 LET A((I*V)+Y)=A((I*V+Y)+Y)
620 NEXT I
630 GOSUB 2090
640 IF R<12.5 THEN PRINT "YOU OBTAINED A POTION."
650 IF R<12.5 THEN PRINT
660 IF R<12.5 THEN LET P=P+Y
670 IF P>O THEN LET P=O
680 GOTO 350
690 REM INCREASE DIFFICULTY
700 GOSUB 2090
710 IF R>=25 THEN RETURN
720 FOR I=V TO M-Y
730 LET A(I+Y)=A(I+Y)*1.5
740 NEXT I
750 PRINT "THE ENEMIES HAVE POWERED UP."
760 PRINT
770 RETURN
780 REM MENU LOOP
790 PRINT
791 CLS
800 PRINT A(Y);"/";A(X);" HP"
810 PRINT A(3);"/";A(H);" MP"
820 PRINT
830 PRINT "(A) ATTACK"
840 PRINT "(M) MAGIC"
850 PRINT "(I) ITEMS"
860 PRINT "(Q) QUIT"
861 SLOW
870 LET A$=INKEY$
871 IF A$="" THEN GOTO 870
872 FAST
880 IF A$="A" OR A$="a" THEN GOTO 930
890 IF A$="M" OR A$="m" THEN GOTO 990
900 IF A$="I" OR A$="i" THEN GOTO 1100
910 IF A$="Q" OR A$="q" THEN GOTO 2480
920 GOTO 780
930 REM ATTACK INPUT ROUTINE
940 GOSUB 2120
950 IF C=Z THEN GOTO 780
960 IF C>X THEN GOTO 930
970 IF C<Z THEN GOTO 930
980 GOTO 1190
990 REM MAGIC INPUT ROUTINE
1000 PRINT
1010 PRINT "(1) LIGHTNING - ";A(M+Y);" MP"
1020 PRINT "(2) CURE - ";A(M+H);" MP"
1030 PRINT "WHICH SPELL DO YOU WANT TO CAST?"
1040 PRINT "TYPE (0) TO GO BACK."
1041 SLOW
1050 LET A$=INKEY$
1051 IF A$="" THEN GOTO 1050
1052 LET C=VAL A$
1053 FAST
1054 CLS
1060 IF C=Y THEN GOTO 1630
1070 IF C=X THEN GOTO 1850
1080 IF C=Z THEN GOTO 780
1090 GOTO 990
1100 REM ITEM ROUTINE
1110 PRINT
1120 PRINT "(1) POTION - ";P
1130 PRINT "WHICH ITEM DO YOU WISH TO USE?"
1140 PRINT "TYPE (0) TO GO BACK."
1141 SLOW
1150 LET A$=INKEY$
1151 IF A$="" THEN GOTO 1150
1152 LET C=VAL A$
1153 FAST
1154 CLS
1160 IF C=Y THEN GOTO 1980
1170 IF C=Z THEN GOTO 780
1180 GOTO 1100
1190 REM ATTACK ROUTINE
1200 LET C=((A((A*V+10)+Y)/H)+(A((A*V+5)+Y)))+A((A*V+7)+Y)-A((T*V+7)+Y)
1210 GOSUB 2090
1220 LET I=A((A*V+11)+Y)
1230 GOSUB 2270
1240 IF D>R THEN LET C=N
1250 LET I=A((T*V+11)+Y)
1260 GOSUB 2270
1270 IF D>R THEN LET C=N
1280 IF R<C THEN GOTO 1330
1290 IF T>Z THEN PRINT "YOUR ATTACK MISSED."
1300 IF T=Z THEN PRINT "SOLDIER ";A;" MISSED YOU."
1310 PRINT
1320 GOTO 420
1330 REM PHYSICAL DAMAGE ROUTINE
1340 LET D=A((A*V+H)+Y)
1350 LET E=A((A*V+12)+Y)
1360 LET C=D+((D+E)/L)*((D*E)/L)
1370 LET D=((U*(K-A((T*V+Q)+Y)))*C)/(U*K)
1380 LET C=(A((A*V+11)+Y)+A((A*V+12)+Y)-A((T*V+12)+Y))/H
1390 GOSUB 2300
1400 IF R<=C THEN GOTO 1420
1410 GOTO 1440
1420 REM CRITICAL HIT DAMAGE
1429 LET TE=D*X
1430 LET D=INT TE
1440 REM RANDOM DAMAGE VARIATION
1449 LET TE=D*(S+(RND*N))/B
1450 LET D=INT TE
1460 IF D=Z THEN LET D=Y
1470 LET A((T*V)+Y)=A((T*V)+Y)-D
1480 IF A((T*V)+Y)<=Z THEN LET A((T*V)+Y)=Z
1490 IF A<>Z THEN GOTO 1560
1500 PRINT "YOU HIT SOLDIER ";T;" FOR ";D;" HP."
1510 PRINT
1520 IF A((T*V)+Y)<>Z THEN GOTO 1550
1530 PRINT "YOU DEFEATED SOLDIER ";T
1540 PRINT
1550 GOTO 420
1560 IF D>Z THEN GOSUB 2330
1570 PRINT "SOLDIER ";A;" HIT YOU FOR ";D;" HP."
1580 PRINT
1590 IF A(Y)<>Z THEN GOTO 1620
1600 PRINT "YOU DIED."
1610 PRINT
1620 GOTO 420
1630 REM LIGHTNING ROUTINE
1640 LET E=Z
1650 LET D=Z
1660 IF A(M+Y)>A(3) THEN GOTO 1810
1670 LET A(3)=A(3)-A(M+Y)
1680 GOSUB 2120
1690 IF C=Z THEN GOTO 990
1700 IF C>X THEN GOTO 1630
1710 IF C<Z THEN GOTO 1630
1720 LET C=A(M+3)+A((A*V+12)+Y)-((A((T*V+12)+Y))/X)-Y
1730 GOSUB 2090
1740 IF R<C THEN GOSUB 2380
1750 IF R>C THEN GOTO 1770
1760 LET D=(A(M+X)*(K-A((T*V+9)+Y))*C)/(U*K)
1770 IF D>Z THEN GOTO 1440
1780 PRINT "YOUR SPELL MISSED."
1790 PRINT
1800 GOTO 420
1810 REM NOT ENOUGH MP ROUTINE
1820 PRINT
1830 PRINT "YOU DON,T HAVE ENOUGH MP TO CAST THAT SPELL."
1840 GOTO 990
1850 REM CURE ROUTINE
1860 LET E=3
1870 IF A(M+H)>A(3) THEN GOTO 1810
1880 LET A(3)=A(3)-A(M+H)
1890 GOSUB 2380
1900 LET D=C+22*A(M+Y+E+Y)
1909 LET TE=D*(S+(RND*N))/B
1910 LET D=INT TE
1920 PRINT
1930 PRINT "YOU HAVE BEEN HEALED FOR ";D;" HP."
1940 PRINT
1950 LET A(Y)=A(Y)+D
1960 IF A(Y)>A(X) THEN LET A(Y)=A(X)
1970 GOTO 420
1980 REM ITEM ROUTINE
1990 IF P=Z THEN PRINT
2000 IF P=Z THEN PRINT "YOU HAVE NO POTIONS."
2010 IF P=Z THEN GOTO 780
2020 LET A(Y)=A(Y)+G
2030 IF A(Y)>A(X) THEN LET A(Y)=A(X)
2040 LET P=P-Y
2050 PRINT
2060 PRINT "YOU HAVE BEEN HEALED FOR 100 HP."
2070 PRINT
2080 GOTO 420
2090 REM RANDOM 100 ROUTINE
2099 LET TE=RND*G
2100 LET R=INT TE
2110 RETURN
2120 REM SHARED INPUT ROUTINE
2130 PRINT
2140 FOR I=Y TO X
2150 IF A((V*I)+Y)>Z THEN PRINT "(";I;")";" SOLDIER ";I
2160 NEXT I
2170 PRINT "WHICH ENEMY DO YOU WANT TO TARGET?"
2180 PRINT "(0) TO GO BACK."
2181 SLOW
2190 LET A$=INKEY$
2191 IF A$="" THEN GOTO 2190
2192 LET C=VAL A$
2193 FAST
2194 CLS
2200 IF C=Z THEN RETURN
2210 LET E=Z
2220 IF A((V*C)+Y)<=Z THEN LET E=N
2230 IF E=N THEN GOTO 2120
2240 LET T=C
2250 PRINT
2260 RETURN
2270 REM LUCKY HIT ROUTINE
2280 LET D=I/H
2290 RETURN
2300 REM RANDOM 65535 ROUTINE
2310 LET R=((RND*F)*O/F)+Y
2320 RETURN
2330 REM DING ROUTINE
2340 FOR J=Z TO G
2350 NEXT J
2360 REM DING.
2370 RETURN
2380 REM MAGIC DAMAGE ROUTINE
2390 LET C=Q*(A(9)+A(V))
2400 RETURN
2410 REM GAME OVER ROUTINE
2420 PRINT
2430 PRINT "GAME OVER"
2440 PRINT
2450 FOR I=Z TO Y
2460 GOSUB 2330
2470 NEXT I
2480 PRINT
2490 PRINT "YOU WON ";W;" TIMES."
2500 PRINT
2510 STOP
2520 LET D$="314,314,54,54,38,96,24,1,21,17,6,14,6,30,30,0,0,6,1,4,1,1,1,50,4,2,30,30,0,0,6,1,4,1,1,1,50,4,2,4,8,100,5,5,255,"
2530 LET C=Z
2540 LET E=Y
2545 LET I=45
2550 LET T$=""
2560 LET C=C+Y
2570 IF D$(C)="," THEN GOTO 2600
2580 LET T$=T$+D$(C)
2590 GOTO 2560
2600 LET A(E)=VAL T$
2610 LET E=E+Y
2620 IF E>I THEN GOTO 315
2630 GOTO 2550
2640 SAVE "BATLTEST"
2650 GOTO 1